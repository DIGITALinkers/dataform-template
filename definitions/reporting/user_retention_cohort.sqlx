config {
  type: "table",
  schema: "reporting",
  name: "user_retention_cohort"
}

WITH first_sessions AS (
  SELECT
    user_pseudo_id,
    MIN(date_partition) AS cohort_date
  FROM ${ref("user_flags")}
  GROUP BY user_pseudo_id
),
user_sessions AS (
  SELECT
    s.user_pseudo_id,
    f.cohort_date,
    s.date_partition,
    DATE_DIFF(s.date_partition, f.cohort_date, DAY) AS days_since_first
  FROM ${ref("user_flags")} s
  JOIN first_sessions f
  ON s.user_pseudo_id = f.user_pseudo_id
)
SELECT
  cohort_date,
  days_since_first,
  COUNT(DISTINCT user_pseudo_id) AS retained_users
FROM user_sessions
GROUP BY cohort_date, days_since_first
ORDER BY cohort_date, days_since_first
